Class Day04

    Option Explicit
    '@PredeclaredId
    '@Exposed
    Private Const Today                         As String = "\2023\Day04.txt"
    
    Private Type State
        Data                                    As Collection
    End Type

    Private s                                   As State

    
    Public Sub Execute()
        
        Part01
        Part02
            
    End Sub

 
    Private Sub Part01()

        Initialise
        
        Dim myResult As Long = 0
        Dim myCard As Variant
        For Each myCard In s.Data
            myResult += Score(WinningNumbers(myCard))
        Next
        
        Fmt.Dbg "The answer to Day {0} part 1 is {1}.  Found is {2}", VBA.Mid$(Today, 10, 2), "15268", myResult
        
    End Sub
    
    
    Private Sub Part02()
        
        Initialise
        
        Dim myResult As Long = 0
        Dim mySumCards As Collection = New Collection
        Dim myKey As Variant
        For Each myKey In s.Data.Keys
        mySumCards.Add Array(myKey, s.Data.Item(myKey))
        Next
        
        Dim myCard As Variant
        Dim myIndex As Long = 1
        Do While myIndex <= mySumCards.Count
        	
        	Dim myWins As Long = WinningNumbers(s.Data.Item(mySumCards.Item(myIndex)(0)))
         	If myWins = 0 Then
            	myIndex += 1
                Continue Do
         	End If
            
            Dim myCardNo As Long = VBA.CLng(Split(mySumCards.Item(myIndex)(0))(1))
            Dim myOffset As Long
            For myOffset = 1 To myWins
            	myKey = "Card " & VBA.CStr(myCardNo + myOffset)
                mySumCards.Add Array(myKey, s.Data.Item(myKey))
            Next
            
            myIndex += 1
        Loop
        
        myResult = mySumCards.Count
        
        Fmt.Dbg "The answer to Day {0} part 2 is {1}.  Found is {2}", VBA.Mid$(Today, 10, 2), "6283755", myResult

    End Sub


    Private Sub Initialise()
    	
        Dim myData As Variant = GetFileAsArrayOfStrings(AoCData & Today)
        
        ' Build s.Data as a keyed collection of string 'Card No' vs array of (array of string)
        Set s.Data = New Collection
        Dim myStr As Variant
        For Each myStr In myData
        	Dim myCard As String = Strs.Replacer(VBA.CStr(myStr), "  ", " ")
            myCard = Replace(myCard, ": ", "|")
            myCard = Replace(myCard, " | ", "|")
            Dim myA As Variant = Split(myCard, "|")
            s.Data.Add key:= myA(0), Item:=Array(ToKeyedCollection(Split(myA(1))), ToKeyedCollection(Split(myA(2))))
        Next
        	
     End Sub
     
     
     Public Function Score(ByVal ipNumber As Long) As Long
     	
     	If ipNumber < 2 Then
        	Return ipNumber
     	End If
        
     	Dim myScore As Long = 1
        Dim myCount As Long
        For myCount = 2 To ipNumber
        	myScore *= 2
        Next
       
        Return myScore
        
     End Function
     
     
     Private Function WinningNumbers(ByRef ipArray As Variant) As Long
     	
         Dim myCount As Long = 0
         Dim myWinningNumber As Variant
         For Each myWinningNumber In ipArray(0).Keys
             If ipArray(1).exists(myWinningNumber) Then
                myCount += 1
             End If
         Next
        
         Return myCount
         
     End Function
    
    
    Private Function ToKeyedCollection(ByRef ipArray As Variant) As Collection
    	Dim myC As Collection = New Collection
        Dim myItem As Variant
        For Each myItem In ipArray
        	myC.Add Key:=myItem, Item:=myC.Count
        Next
        Return myC
    End Function
    
End Class